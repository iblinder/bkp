#!/usr/bin/env perl6
use YAMLish;
use Bkp;

sub load-class ( Str:D $namespace, Str:D $class ) {
    my $package = "Bkp::{$namespace.lc.tc}::{$class.lc.tc}";
    require ::($package);
    return ::($package);
}

sub add-middleware ( $src, $namespace, %profile ) {
    return $src if !%profile{$namespace};
    return load-class( $namespace, %profile{$namespace}<class>:delete )
         . new( |%profile, src => $src );
}

sub run-profile (%profile) {
    my $src = load-class( 'src', %profile<src><class>:delete )
            . new( |%profile<src> );
    $src = add-middleware $src, 'compress', %profile;
    $src = add-middleware $src, 'crypt', %profile;
    $src = add-middleware $src, 'shape', %profile;
    my $dst = load-class( 'dst', %profile<dst><class> )
            . new( |%profile<dst>, src => $src );
    $dst.SEND;
}

multi MAIN (*@filenames) {
    for @filenames -> FilePath $filename {
        my %profile = load-yaml $filename.IO.slurp;
        my $prefix = $filename ~~ rx{:i (<-[ / ]>+) \. ya?ml $}
          ?? ~$0 !! $filename.IO.basename.lc;
        %profile<dst><prefix> //= $prefix;
        run-profile %profile;
    }
}

